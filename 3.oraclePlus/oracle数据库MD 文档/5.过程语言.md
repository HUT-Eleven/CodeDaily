# PL/SQL语言

### 1.PL/SQL简介

#### 1.1 什么是PL/SQL

> ​	PL/SQL也是一种程序语言，叫做过程化SQL语言（Procedural Language/Structured Query Language）。是Oracle数据库对SQL语句的扩展。在普通SQL语句的使用上增加了编程语言的特点，所以PL/SQL就是把数据操作和查询语句组织在PL/SQL代码的过程性单元中，通过逻辑判断、循环等操作实现复杂的功能或者计算的程序语言。

#### 1.2 为什么使用PL/SQL

> ​	使用PL/SQL可以编写具有很多高级功能的程序，虽然通过多个SQL语句可能也能实现同样的功能，但是相比而言，PL/SQL具有更为明显的一些优点：
>
> 1.能够使一组SQL语句的功能更具模块化程序特点；
>
> 2.采用了过程性语言控制程序的结构；
>
> 3.可以对程序中的错误进行自动处理，使程序能够在遇到错误的时候不会被中断；
>
> 4.具有较好的可移植性，可以移植到另一个Oracle数据库中；
>
> 5.集成在数据库中，调用更快；
>
> 6.减少了网络的交互，有助于提高程序性能

### 2.PL/SQL语法

#### 2.1 语法结构

> PL/SQL是一种块结构的语言，这意味着PL/SQL程序被划分和编写代码的逻辑块。每块由三个子部分组成,其基本语法结构如下：
>
> ```sql
> declare
>   --定义部分，可以定义过程中需要使用到的变量
>   --每个变量定义后都需要分号结尾
> begin
>   --过程内容，用来实现业务操作
> exception
>   --处理过程中的异常
> end;
> ```
>
> 其中，begin，end 是必写的，其他的可以省略
>
> ​	建议打开【命令窗口】，用来编写过程语句
>
> 如果需要在控制台输出信息，需要打开serveroutput,打开命令为：
>
> ```sql
> set serveroutput on;
> ```
>
> 

#### 2.2 变量定义

> 见如下代码：
>
> ```sql
> declare
>   emp_no  number(8) :=1001;
>   e_name  varchar2(50);
>   e_birthday date :=sysdate;
> begin
>   e_name := 'zhangsan';
>   dbms_output.put_line('emp_no:'||emp_no||',e_name:'||e_name||',e_birthday:'||to_char(e_birthday,'yyyy-mm-dd hh24:mi:ss'));
> end;
> ```
>
> 其中，对于变量名，要求：
>
> ​         1.只能是字母，数字，下划线，$,#
>
> ​         2.首字母必须是字母
>
> ​         3.长度不允许超过30
>
> ​         4.不允许使用关键字
>
> 在begin中，是不允许定义新的变量的;
>
> 在变量定义时，可以通过:=的方式给定默认值，也可以不给定；
>
> 分隔符是一个具有特殊意义的符号。以下是在PL/SQL分隔符的列表：
>
> | 分隔符                | 描述             |
> | ------------------ | -------------- |
> | **+, -, \*, /**    | 加法，减法/否定，乘法，除法 |
> | **%**              | 属性索引           |
> | **'**              | 字符串分隔符         |
> | **.**              | 组件选择           |
> | **(,)**            | 表达式或列表分隔符      |
> | **:**              | 主机变量指示符        |
> | **,**              | 项目分离符          |
> | **"**              | 带引号的标识符分隔符     |
> | **=**              | 关系运算符          |
> | **@**              | 远程访问指示符        |
> | **;**              | 声明终止符          |
> | **:=**             | 赋值运算符          |
> | **=>**             | 操作符关联          |
> | **\|\|**           | 连接运算符          |
> | ***\***            | 乘方运算符          |
> | **<<, >>**         | 标签分隔符（开始和结束）   |
> | **/\*, */**        | 多行注释分隔符（开始和结束） |
> | **--**             | 单行注释指示符        |
> | **..**             | 范围操作           |
> | **<, >, <=, >=**   | 关系运算符          |
> | **<>, '=, ~=, ^=** | 不同版本的不相等的      |
>
> 

#### 2.3 选择结构

##### 2.3.1 if的用法

> 使用if选择结构的语法如下：
>
> ```sql
> declare
>   v_msg varchar2(50);
> begin
>   --&可以用来弹框并接受用户输入
>   v_msg := '&名字';
>   --if 条件1 then 如果条件成立，要执行的操作
>   --elsif 条件2 then 条件1不成立，条件2成立时，要做的操作
>  -- ...
>   --else 前面所有的条件都不成立时，要执行的操作
>   --end if;
> end;
> ```
>
> if示例代码如下：
>
> ```sql
> declare
>   v_msg varchar2(50);
> begin
>   v_msg := '&名字';
>   if v_msg='zhangsan' then dbms_output.put_line('管理员');
>   elsif v_msg='lisi' then dbms_output.put_line('普通用户');
>   else dbms_output.put_line('什么都不是');
>   end if;
> end;
> ```
>
> 

##### 2.3.2 switch的用法

> 当有多重情况时，也可以使用switch结构：
>
> ```sql
> --接受用户输入，如果输入的是0，输出星期天，如果是1，就输出星期一...
> declare
>   v_num number(8);
>   v_show varchar2(50);
> begin
>   v_num:=&数字;
>   case v_num
>     when 0 then v_show:='星期日';
>     when 1 then v_show:='星期一';
>     when 2 then v_show:='星期二';
>     when 3 then v_show:='星期三';
>     when 4 then v_show:='星期四';
>     when 5 then v_show:='星期五';
>     when 6 then v_show:='星期六';
>     else v_show:='输入错误';
>   end case;
>   dbms_output.put_line(v_show);
> end;
> ```
>
> switch结构还有一种写法(这种写法可以用来判断范围)：
>
> ```sql
> declare
>   v_num number(8);
>   v_show varchar2(50);
> begin
>   v_num:=&数字;
>   case 
>     when v_num=0 then v_show:='星期日';
>     when v_num=1 then v_show:='星期一';
>     when v_num=2 then v_show:='星期二';
>     when v_num=3 then v_show:='星期三';
>     when v_num=4 then v_show:='星期四';
>     when v_num=5 then v_show:='星期五';
>     when v_num=6 then v_show:='星期六';
>     else v_show:='输入错误';
>   end case;
>   dbms_output.put_line(v_show);
> end;
> ```
>
> 

#### 2.4 循环结构

##### 2.4.1 loop循环

> loop循环中，需要我们自定义循环结束的条件，语法如下：
>
> ```sql
> declare
>   --变量定义
> begin
>   loop
>     --循环执行的内容
>     if 条件 then exit; end if;--这里表示循环终止的条件
>   end loop;
> end;
> ```
>
> 如下是1+2+3+...+10求和运算的过程：
>
> ```sql
> --1+2+3+...+10=?
> declare
>   v_num number(8):=1;
>   v_sum number(8):=0;
> begin
>   loop
>     v_sum := v_sum + v_num;
>     dbms_output.put_line('加数为'||v_num||',现在的和是：'||v_sum);
>     v_num := v_num +1;
>     if v_num>10 then exit; end if;
>   end loop;
>   dbms_output.put_line('最后的结果：'||v_sum);
> end;
> ```
>
> 

##### 2.4.2 for循环

> PL/SQL也提供了for循环的语法，语法格式如下：
>
> ```sql
> declare
>   --变量定义
> begin
>   for 新的变量 in 循环范围 loop
>     --循环执行的内容(循环体)
>   end loop;
> end;
> ```
>
> 循环范围可以是   1..10   ,也可以用游标(后续会讲到)
>
> 1+2+3+...+10使用for循环来实现的代码如下：
>
> ```sql
> --1+2+3+...+10=?
> declare
>   v_sum number(8):=0;
> begin
>   for i in 1..10 loop
>     v_sum := v_sum + i;
>     dbms_output.put_line('i的值：'||i||',现在的结果是：'||v_sum);
>   end loop;
>   dbms_output.put_line('最后的结果：'||v_sum);
> end;
> ```
>
> 

##### 2.4.3 while循环

> while循环的语法：
>
> ```sql
> declare
>   --定义变量
> begin
>   while 条件 loop
>     --循环执行的内容
>   end loop;
> end;
> ```
>
> 还是1+2+3+...+10的求和，使用while循环实现如下：
>
> ```sql
> declare
>   v_sum number(8):=0;
>   v_num number(8):=1;
> begin
>   while v_num<=10 loop
>     v_sum := v_sum + v_num;
>     v_num := v_num + 1;
>   end loop;
>   dbms_output.put_line('v_sum:'||v_sum);
> end;
> ```
>
> 

#### 2.5立即执行

> 立即执行的用法，见如下代码：
>
> ```sql
> --立即执行
> declare
>   v_id number(8) :=23;
>   v_name varchar2(50) :='zhangsan';
> begin
>   execute immediate 'create table t_user(userid number(8) primary key,username varchar2(50) not null)';
>   execute immediate 'insert into t_user(userid,username) values(:1,:2)' using v_id,v_name;
>   commit;
> end;
> ```
>
> 利用立即执行语法，循环添加表和数据:
>
> ```sql
> --循环添加表和数据
> declare
>   v_id number(8) :=23;
>   v_name varchar2(50) :='zhangsan';
> begin
>   execute immediate 'create table t_user(userid number(8) primary key,username varchar2(50) not null)';
>   for i in 1..10 loop
>   execute immediate 'insert into t_user(userid,username) values(:1,:2)' using i,v_name;
>   end loop;
>   commit;
> end;
> ```
>
> 

#### 2.6 异常处理

> 对于过程中的语句，也有类似java中的异常处理，对于异常，PL/SQL中提供了一部分预定义异常，如下：
>
> | 预定义异常            | 说  明                     |
> | ---------------- | ------------------------ |
> | ACCESS_INTO_NULL | 视图给一个没有初始化的对象赋值          |
> | DUP_VAL_ON_INDEX | 重复的值存储在使用唯一索引的数据库列中      |
> | INVALID_NUMBER   | 视图将一个非有效的字符串转换成数字        |
> | LOGIN_DENIED     | 使用无效的用户名和口令登录Oracle      |
> | NO_DATA_FOUND    | 语句无返回数据                  |
> | TOO_MANY_ROWS    | 在执行SELECT INTO语句后返回多行时出现 |
>
> 我们也可以在程序中定义自己的异常，见如下代码：
>
> ```sql
> declare
>   v_num number(8):=0;
>   v_sum number(8):=0;
>   temp exception;
> begin
>   loop
>     v_sum := v_sum + v_num;
>     v_num := v_num +1;
>     if v_num>=5 then raise temp; end if;
>     if v_num>10 then exit; end if;
>   end loop;
>   dbms_output.put_line('最后的计算结果：'||v_sum);
> exception
>   when temp then dbms_output.put_line('出了异常了');
>   when others then dbms_output.put_line('出了其他异常');
> end;
> ```
>
> 通常，我们在存储过程或者函数中，通过返回某个编号，来表示出现了某个异常

#### 2.7 游标

> ​	Oracle会创建一个存储区域，被称为上下文区域，用于处理SQL语句，其中包含需要处理的语句，例如所有的信息，行数处理，等等。
>
> ​	游标是指向这一上下文的区域。 PL/SQL通过控制光标在上下文区域。游标持有的行(一个或多个)由SQL语句返回
>
> ​	在实际编码中，游标用来临时存放一个或一组数据。游标的使用分两种：隐式游标和显式游标

##### 2.7.1 隐式游标

> 隐式游标通过在查询语句中的into来实现：
>
> ```sql
> declare
>   v_id  sporter.sporterid%type;--这里表示定义一个变量v_id,它的数据类型和sporter表的sporterid同样的类型
>   v_name sporter.sportername%type;
> begin
>   v_id := &编号;
>   --在过程语句块中，是不允许单独写查询语句的
>   --如果一定需要查询语句，需要把查询结果用游标存储
>   select sportername into v_name from sporter where sporterid=v_id;
>   dbms_output.put_line(v_id||'对应的名字：'||v_name);
> end;
> ```
>
> 如果有多列，也可以使用这种方式：
>
> ```sql
> declare
>   v_id  sporter.sporterid%type;--这里表示定义一个变量v_id,它的数据类型和sporter表的sporterid同样的类型
>   v_name sporter.sportername%type;
>   v_sex sporter.sex%type;
>   v_depart sporter.department%type;
> begin
>   v_id := &编号;
>   --在过程语句块中，是不允许单独写查询语句的
>   --如果一定需要查询语句，需要把查询结果用游标存储
>   select sportername,sex,department into v_name,v_sex,v_depart from sporter where sporterid=v_id;
>   dbms_output.put_line(v_id||'对应的名字：'||v_name||',部门：'||v_depart||',性别：'||v_sex);
> end;
> ```
>
> 也可以使用rowtype来直接装载一行数据：
>
> ```sql
> declare
>   --这里表示定义一个变量v_id,它的数据类型和sporter表的sporterid同样的类型
>   v_id  sporter.sporterid%type;
>   --如下这种方式可以直接装载一行数据
>   v_sporter sporter%rowtype;
> begin
>   v_id := &编号;
>   --在过程语句块中，是不允许单独写查询语句的
>   --如果一定需要查询语句，需要把查询结果用游标存储
>   select sporterid,sportername,sex,department into v_sporter from sporter where sporterid=v_id;
>   dbms_output.put_line(v_id||'对应的名字：'||v_sporter.sportername||',部门：'||v_sporter.department||',性别：'||v_sporter.sex);
> end;
> ```
>
> 

##### 2.7.2 显式游标

> 显式游标的使用分四步操作：
>
> 1.申明游标;
>
> 2.打开游标
>
> 3.提取游标
>
> 4.关闭游标
>
> 使用过程见如下代码：
>
> ```sql
> declare
>   v_id sporter.sporterid%type;
>   cursor sporterinfo is select * from sporter where sporterid=v_id;
>   v_sporter sporter%rowtype;
> begin
>   v_id := &编号;
>   open sporterinfo;
>   fetch sporterinfo into v_sporter;
>   dbms_output.put_line('名字：'||v_sporter.sportername||',性别：'||v_sporter.sex);
>   close sporterinfo;
> end;
> ```
>
> 当查询了多行数据时，采用loop循环来循环游标：
>
> ```sql
> declare
>   v_id sporter.sporterid%type;
>   cursor sporterinfo is select * from sporter;
>   v_sporter sporter%rowtype;
> begin
>   open sporterinfo;
>   loop
>     fetch sporterinfo into v_sporter;
>     if sporterinfo%notfound then exit; end if;
>     dbms_output.put_line('编号：'||v_sporter.sporterid||',名字：'||v_sporter.sportername||',性别：'||v_sporter.sex);
>   end loop;
>   if sporterinfo%isopen then close sporterinfo; end if;
> end;
> ```
>
> 此时用到了游标属性%notfound,常见游标属性还有：
>
> | 属性名称      | 说  明                                     |
> | --------- | ---------------------------------------- |
> | %found    | 用于检验游标是否成功，通常在FETCH语句前使用，当游标按照条件查询出一条记录时，返回true |
> | %isopen   | 判断游标是否处于打开状态，视图打开一个已经打开或者已经关闭的游标，将会出现错误  |
> | %notfound | 与%found的作用相反，当按照条件无法查询到记录时，返回true        |
> | %rowcount | 循环执行游标读取数据时，返回检索出的记录数据的行数                |
>
> 也可以利用for循环来提取游标:
>
> ```sql
> declare
>   cursor sporterinfo is select * from sporter;
> begin
>   for i in sporterinfo loop
>     dbms_output.put_line('编号：'||i.sporterid||',名字：'||i.sportername||',性别：'||i.sex);
>   end loop;
> end;
> ```
>
> 还可以通过游标来修改数据：
>
> ```sql
> --通过游标修改数据
> declare
>       cursor userinfo is select * from sporter where sporterid=1001 for update;
>       v_name varchar2(50);
> begin
>       for i in userinfo loop
>         v_name :=i.sportername;
>         update sporter set sportername='王五' where current of userinfo;
>         dbms_output.put_line('用户信息：'||i.sporterid||'-------'||i.sportername);
>       end loop;
>       commit;
> end;
> ```
>
> 